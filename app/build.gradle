apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: "androidx.navigation.safeargs.kotlin"

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 4
ext.minimumSdkVersion = 21

android {
    compileSdkVersion 29

    signingConfigs {
        config {
            keyAlias ALIAS_HOMC_TRUCK
            keyPassword PASSWORD_ALIAS_HOMC_TRUCK
            storeFile file(FILE_KEYSTORE_HOMC_TRUCK)
            storePassword PASSWORD_KEYSTORE_HOMC_TRUCK
        }
    }

    packagingOptions {
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }

    defaultConfig {
        applicationId "com.homc.homctruck"
        targetSdkVersion 29
        minSdkVersion project.ext.minimumSdkVersion
        versionCode generateVersionCode() // 19|01|02|03 MaxVal = 2100000000
        versionName generateVersionName() // v1.2.3
        multiDexEnabled true

        /**
         * When localizing the application to a new language,
         * dont forget to enable the language here.
         */
        resConfigs "en"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        javaMaxHeapSize "6g"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            versionNameSuffix = '-production'

            signingConfig signingConfigs.config

            buildConfigField "String", "SERVER_URL", "\"https://us-central1-homctruck.cloudfunctions.net/\""
            resValue "string", "authority_provider", "\"com.homc.homctruck.fileprovider\""
            resValue "string", "app_name", "\"HOMC Truck\""
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix '.debug'
            // User facing version name suffix that we

            // usually show in About section of the app
            versionNameSuffix = '-staging'

            buildConfigField "String", "SERVER_URL", "\"https://us-central1-homctruck.cloudfunctions.net/\""
            resValue "string", "authority_provider", "\"com.homc.homctruck.debug.fileprovider\""
            resValue "string", "app_name", "\"St. HOMC Truck\""

            // Disable Crashlytics build time ID generation to speed
            // up compilation. More info:
            // https://developer.android.com/studio/build/optimize-your-build#disable_crashlytics
            ext.alwaysUpdateBuildId = false

            // Disables PNG crunching on DEBUG build type
            // https://blog.mindorks.com/speed-up-gradle-build-for-android-to-save-your-time
            crunchPngs false // Enabled by default for RELEASE build type
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // For example: doctors-social-v1.0.86-staging.apk
            def project = "HOMCTruck"
            def seperator = "-"
            // def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionName

            def newApkName = project + seperator + "v"+ version + ".apk"
            outputFileName = new File(newApkName)

            def valueAccountType = applicationId + '.account'
            buildConfigField "String", "ACCOUNT_TYPE", '"' + valueAccountType + '"'
            resValue "string", "account_type", valueAccountType

            def valueContentAuthority = valueAccountType + '.authority'
            buildConfigField "String", "ACCOUNT_PROVIDER_AUTHORITY", '"' + valueContentAuthority + '"'
            resValue "string", "account_provider_authority", valueContentAuthority

            def valueAccountName = applicationId + '.name'
            buildConfigField "String", "ACCOUNT_NAME", '"' + valueAccountName + '"'
            resValue "string", "account_name", valueAccountName

            resValue "string", "google_apis_places_and_maps", HOMC_MAP_API_KEY
            resValue "string", "payee_upi", HOMC_PAYEE_UPI_ID
            resValue "string", "payee_name", HOMC_PAYEE_NAME
            resValue "string", "payee_merchant_code", HOMC_PAYEE_MARCHANT_CODE
            resValue "string", "payee_money_currency_code", HOMC_PAYEE_MONEY_CURRENCY_CODE
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions { checkReleaseBuilds false }
}

/**
 * Refer https://medium.com/@maxirosson/versioning-android-apps-d6ec171cfd82 for more
 * information on app versioning. We have adapted the information in the link above
 * as per our needs.
 */
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 1000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    def nav_version = "2.3.1"
    def material_version = "1.3.0-alpha03"
    def work_version = "2.4.0"

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.annotation:annotation:1.1.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.9"

    implementation 'com.google.firebase:firebase-messaging:21.0.0'

    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.0'
    implementation 'com.google.firebase:firebase-config:20.0.1'
    implementation 'com.google.firebase:firebase-inappmessaging-display:19.1.2'
    implementation 'com.google.firebase:firebase-auth:20.0.1'
    implementation 'com.google.android.libraries.places:places:2.4.0'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    implementation "com.google.android.material:material:$material_version"

    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$work_version"

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}